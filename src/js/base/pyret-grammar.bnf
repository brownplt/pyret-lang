#lang ragg

/*
  name: PyretGrammar
*/

program: prelude block

end: END | SEMI

prelude: [provide-stmt] [provide-types-stmt] import-stmt*

import-stmt: INCLUDE import-source
import-stmt: IMPORT import-source AS NAME
import-stmt: IMPORT NAME (COMMA NAME)* FROM import-source
import-source: import-special | import-name | import-string 
import-special: NAME PARENNOSPACE STRING (COMMA STRING)* RPAREN
import-name: NAME
import-string: STRING
provide-stmt: PROVIDE stmt end | PROVIDE STAR
provide-types-stmt: PROVIDE-TYPES record-ann | PROVIDE-TYPES STAR

block: stmt*

stmt: type-expr | newtype-expr
    | let-expr | fun-expr | data-expr | when-expr
    | var-expr | rec-expr | assign-expr | check-test | check-expr
    | contract-stmt

type-expr: TYPE NAME EQUALS ann
newtype-expr: NEWTYPE NAME AS NAME
let-expr: toplevel-binding EQUALS binop-expr
binding: [SHADOW] NAME [COLONCOLON ann]
toplevel-binding: binding 
# toplevel-binding: [SHADOW] NAME COLONCOLON noparen-arrow-ann
multi-let-expr: LET let-binding-elt* let-binding COLON block end
let-binding-elt: let-binding COMMA
let-binding: let-expr | var-expr
letrec-expr: LETREC letrec-binding* let-expr COLON block end
letrec-binding: let-expr COMMA

type-bind: NAME EQUALS ann
newtype-bind: NEWTYPE NAME AS NAME

type-let-bind: type-bind | newtype-bind
type-let-bind-elt: type-let-bind COMMA
type-let-expr: TYPE-LET type-let-bind-elt* type-let-bind COLON block end

contract-stmt: NAME COLONCOLON (ann | noparen-arrow-ann)

fun-expr: FUN NAME fun-header COLON doc-string block where-clause end
fun-header: ty-params args return-ann
ty-params:
  [(LANGLE|LT) list-ty-param* NAME (RANGLE|GT)]
list-ty-param: NAME COMMA
args: PARENNOSPACE [list-arg-elt* binding] RPAREN
list-arg-elt: binding COMMA
return-ann: [THINARROW ann]
doc-string: [DOC STRING]
where-clause: [WHERE block]

check-expr: CHECK STRING COLON block end
check-expr: (CHECKCOLON|EXAMPLESCOLON) block end
check-test: binop-expr check-op [PERCENT (PARENSPACE|PARENNOSPACE) binop-expr RPAREN] binop-expr
          | binop-expr check-op-postfix
          | binop-expr


data-expr: DATA NAME ty-params data-mixins COLON [first-data-variant] data-variant* data-sharing where-clause end
data-mixins: [DERIVING mixins]
variant-constructor: NAME variant-members
first-data-variant: variant-constructor data-with | NAME data-with
data-variant: BAR variant-constructor data-with | BAR NAME data-with
variant-members: PARENNOSPACE [list-variant-member* variant-member] RPAREN
list-variant-member: variant-member COMMA
variant-member: [REF] binding
data-with: [WITH fields]
data-sharing: [SHARING fields]

mixins: list-mixin* binop-expr
list-mixin: binop-expr COMMA

var-expr: VAR toplevel-binding EQUALS binop-expr
rec-expr: REC toplevel-binding EQUALS binop-expr
assign-expr: NAME COLONEQUALS binop-expr

when-expr: WHEN binop-expr COLON block end

binop-expr: expr (binop expr)*

binop: PLUS | DASH | STAR | SLASH | LEQ | GEQ | EQUALEQUAL | SPACESHIP | EQUALTILDE
     | NEQ  | LT  | GT | AND | OR | CARET

check-op: IS | ISEQUALEQUAL | ISEQUALTILDE | ISSPACESHIP
        | ISNOT | ISNOTEQUALEQUAL | ISNOTEQUALTILDE | ISNOTSPACESHIP
        | RAISES | RAISESOTHER
        | SATISFIES | SATISFIESNOT
        | RAISESSATISFIES | RAISESVIOLATES

check-op-postfix: RAISESNOT

expr: paren-expr | id-expr | prim-expr
    | lambda-expr | method-expr | app-expr
    | obj-expr
    | dot-expr
#    | bracket-expr NOTE(joe): commented out until it has semantics
    | get-bang-expr | update-expr
    | extend-expr
    | if-expr | if-pipe-expr | cases-expr
    | for-expr
    | user-block-expr | inst-expr
    | multi-let-expr | letrec-expr
    | type-let-expr
    | construct-expr

bad-expr: UNTERMINATED-STRING | UNTERMINATED-BLOCK-COMMENT | BAD-OPER | BAD-NUMBER | UNKNOWN

# paren-exprs must be preceded by a space, so as not be be confused with
# function application
paren-expr: PARENSPACE binop-expr RPAREN

id-expr: NAME

prim-expr: num-expr | frac-expr | bool-expr | string-expr

num-expr: NUMBER
frac-expr: RATIONAL
bool-expr: TRUE | FALSE
string-expr: STRING

lambda-expr: LAM fun-header COLON doc-string block where-clause end

method-expr: METHOD fun-header COLON doc-string block where-clause end

app-expr: expr app-args
# application must have the function expression immediately adjacent to
# the argument list, so as not to be confused with parenthesized exprs
app-args: PARENNOSPACE [app-arg-elt* binop-expr] RPAREN
app-arg-elt: binop-expr COMMA

# at least one annotation must be provided
inst-expr: expr LANGLE inst-elt* ann RANGLE
inst-elt: ann COMMA

obj-expr: LBRACE obj-fields RBRACE | LBRACE RBRACE
obj-fields: list-obj-field* obj-field [COMMA]
list-obj-field: obj-field COMMA
obj-field: key COLON binop-expr
     | REF key [COLONCOLON ann] COLON binop-expr
     | key fun-header COLON doc-string block where-clause end

fields: list-field* field [COMMA]
list-field: field COMMA
field: key COLON binop-expr
     | key fun-header COLON doc-string block where-clause end
key: NAME

construct-expr: LBRACK construct-modifier binop-expr COLON [list-elt* binop-expr] RBRACK
construct-modifier: | LAZY
list-elt: binop-expr COMMA

dot-expr: expr DOT NAME
bracket-expr: expr DOT LBRACK binop-expr RBRACK

get-bang-expr: expr BANG NAME

extend-expr: expr DOT LBRACE fields RBRACE
update-expr: expr BANG LBRACE fields RBRACE

if-expr: IF binop-expr COLON block else-if* [ELSECOLON block] end
else-if: ELSEIF binop-expr COLON block
if-pipe-expr: ASKCOLON if-pipe-branch* [BAR OTHERWISECOLON block] end
if-pipe-branch: BAR binop-expr THENCOLON block

cases-binding: [REF] binding
list-cases-arg-elt: cases-binding COMMA
cases-args: PARENNOSPACE [list-cases-arg-elt* cases-binding] RPAREN
cases-expr: CASES (PARENSPACE|PARENNOSPACE) ann RPAREN binop-expr COLON cases-branch* [BAR ELSE THICKARROW block] end
cases-branch: BAR NAME [cases-args] THICKARROW block

for-bind: binding FROM binop-expr
for-bind-elt: for-bind COMMA
for-expr: FOR expr PARENNOSPACE [for-bind-elt* for-bind] RPAREN return-ann COLON block end

user-block-expr: BLOCK block end

ann: name-ann | record-ann | arrow-ann | app-ann | pred-ann | dot-ann

name-ann: NAME
record-ann: LBRACE [list-ann-field* ann-field] RBRACE
list-ann-field: ann-field COMMA
ann-field: NAME COLONCOLON ann

noparen-arrow-ann: [arrow-ann-elt* ann] THINARROW ann
arrow-ann: (PARENSPACE|PARENNOSPACE) [arrow-ann-elt* ann] THINARROW ann RPAREN
arrow-ann-elt: ann COMMA

app-ann: (name-ann|dot-ann) LANGLE app-ann-elt* ann RANGLE
app-ann-elt: ann COMMA

pred-ann: ann PERCENT (PARENSPACE|PARENNOSPACE) id-expr RPAREN

dot-ann : NAME DOT NAME
