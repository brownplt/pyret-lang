#lang ragg

/*
  name: BootstrapGrammar
*/

program: prelude block

end: END | SEMI

prelude: [provide-stmt] import-stmt*

import-stmt: IMPORT (import-name | import-string) AS NAME
import-stmt: IMPORT NAME (COMMA NAME)* FROM (import-name | import-string)
import-name: NAME
import-string: STRING
provide-stmt: PROVIDE stmt end | PROVIDE STAR

block: stmt*

stmt: let-expr | fun-expr | data-expr | datatype-expr | when-expr
    | var-expr | assign-expr | check-test | check-expr
    | graph-expr | contract-stmt

let-expr: toplevel-binding EQUALS binop-expr
# NEW IN BOOTSTRAP
let-expr: VAL toplevel-binding EQUALS binop-expr
binding: [SHADOW] NAME [COLONCOLON ann]
toplevel-binding: binding | [SHADOW] NAME COLONCOLON noparen-arrow-ann
multi-let-expr: LET let-binding-elt* let-binding COLON block end
let-binding-elt: let-binding COMMA
let-binding: let-expr | var-expr
letrec-expr: LETREC letrec-binding* let-expr COLON block end
letrec-binding: let-expr COMMA

contract-stmt: NAME COLONCOLON (ann | noparen-arrow-ann)

fun-expr: FUN fun-header COLON doc-string block where-clause end
fun-header: ty-params NAME args return-ann
ty-params:
  [(LANGLE|LT) list-ty-param* NAME (RANGLE|GT)]
list-ty-param: NAME COMMA
args: (PARENSPACE|PARENNOSPACE) [list-arg-elt* binding] RPAREN
list-arg-elt: binding COMMA
return-ann: [THINARROW ann]
doc-string: [DOC STRING]
where-clause: [WHERE block]
# NEW IN BOOTSTRAP
where-clause: EXAMPLESCOLON block

check-expr: CHECK STRING COLON block end
check-expr: CHECKCOLON block end
# NEW IN BOOTSTRAP
check-expr: EXAMPLES STRING COLON block end
check-expr: EXAMPLESCOLON block end
check-test: binop-expr check-op binop-expr | binop-expr

data-expr: DATA NAME ty-params data-mixins COLON [first-data-variant] data-variant* data-sharing where-clause end
data-mixins: [DERIVING mixins]
variant-constructor: NAME variant-members
first-data-variant: variant-constructor data-with | NAME data-with
data-variant: BAR variant-constructor data-with | BAR NAME data-with
variant-members: (PARENSPACE|PARENNOSPACE) [list-variant-member* variant-member] RPAREN
list-variant-member: variant-member COMMA
variant-member: [MUTABLE|CYCLIC] binding
data-with: [WITH fields]
data-sharing: [SHARING fields]

mixins: list-mixin* binop-expr
list-mixin: binop-expr COMMA

datatype-expr: DATATYPE NAME ty-params COLON [first-datatype-variant] datatype-variant* where-clause end
first-datatype-variant: variant-constructor constructor-clause | NAME constructor-clause
datatype-variant: BAR variant-constructor constructor-clause | BAR NAME constructor-clause
constructor-clause: WITHCONSTRUCTOR (PARENSPACE|PARENNOSPACE) NAME RPAREN COLON block end

var-expr: VAR toplevel-binding EQUALS binop-expr
assign-expr: NAME COLONEQUALS binop-expr

graph-expr: GRAPH let-expr* end

when-expr: WHEN binop-expr COLON block end

binop-expr: expr (binop expr)*

binop: PLUS | DASH | STAR | SLASH | LEQ | GEQ | EQUALEQUAL
     | NEQ  | LT  | GT | AND | OR | CARET

check-op: IS | RAISES | SATISFIES

expr: paren-expr | id-expr | prim-expr
    | lambda-expr | method-expr | app-expr
    | obj-expr
    | dot-expr | bracket-expr
    | get-bang-expr | update-expr
    | extend-expr
    | if-expr | if-pipe-expr | cases-expr
    | for-expr | try-expr
    | user-block-expr | inst-expr
    | multi-let-expr | letrec-expr
    | construct-expr

bad-expr: UNTERMINATED-STRING | UNKNOWN

# paren-exprs must be preceded by a space, so as not be be confused with
# function application
paren-expr: PARENSPACE binop-expr RPAREN

id-expr: NAME

prim-expr: num-expr | frac-expr | bool-expr | string-expr

num-expr: NUMBER
frac-expr: RATIONAL
bool-expr: TRUE | FALSE
string-expr: STRING

lambda-expr: LAM ty-params args return-ann COLON doc-string block where-clause end

method-expr: METHOD args return-ann COLON doc-string block where-clause end

app-expr: expr app-args
# application must have the function expression immediately adjacent to
# the argument list, so as not to be confused with parenthesized exprs
app-args: PARENNOSPACE [app-arg-elt* binop-expr] RPAREN
app-arg-elt: binop-expr COMMA

# at least one annotation must be provided
inst-expr: expr LANGLE inst-elt* ann RANGLE
inst-elt: ann COMMA

obj-expr: LBRACE obj-fields RBRACE | LBRACE RBRACE
obj-fields: list-obj-field* obj-field [COMMA]
list-obj-field: obj-field COMMA
obj-field: key COLON binop-expr
     | MUTABLE key [COLONCOLON ann] COLON binop-expr
     | key args return-ann COLON doc-string block where-clause end

fields: list-field* field [COMMA]
list-field: field COMMA
field: key COLON binop-expr
     | key args return-ann COLON doc-string block where-clause end
key: STRING | NAME

construct-expr: LBRACK construct-modifier binop-expr COLON [list-elt* binop-expr] RBRACK
construct-modifier: | LAZY
list-elt: binop-expr COMMA

dot-expr: expr DOT NAME
bracket-expr: expr DOT LBRACK binop-expr RBRACK

get-bang-expr: expr BANG NAME

extend-expr: expr DOT LBRACE fields RBRACE
update-expr: expr BANG LBRACE fields RBRACE

if-expr: IF binop-expr COLON block else-if* [ELSECOLON block] end
else-if: ELSEIF binop-expr COLON block
if-pipe-expr: ASKCOLON if-pipe-branch* [BAR OTHERWISECOLON block] end
if-pipe-branch: BAR binop-expr THENCOLON block

cases-expr: CASES (PARENSPACE|PARENNOSPACE) ann RPAREN expr COLON cases-branch* [BAR ELSE THICKARROW block] end
cases-branch: BAR NAME [args] THICKARROW block

for-bind: binding FROM binop-expr
for-bind-elt: for-bind COMMA
for-expr: FOR expr PARENNOSPACE [for-bind-elt* for-bind] RPAREN return-ann COLON block end

try-expr: TRY  block EXCEPT (PARENSPACE|PARENNOSPACE) binding RPAREN COLON block end

user-block-expr: BLOCK block end

ann: name-ann | record-ann | arrow-ann | app-ann | pred-ann | dot-ann

name-ann: NAME
record-ann: LBRACE [list-ann-field* ann-field] RBRACE
          | LBRACE RBRACE
list-ann-field: ann-field COMMA
ann-field: NAME COLON ann | NAME COLONCOLON ann

noparen-arrow-ann: [arrow-ann-elt* ann] THINARROW ann
arrow-ann: (PARENSPACE|PARENNOSPACE) [arrow-ann-elt* ann] THINARROW ann RPAREN
arrow-ann-elt: ann COMMA

app-ann: (name-ann|dot-ann) LANGLE app-ann-elt* ann RANGLE
app-ann-elt: ann COMMA

pred-ann: ann PERCENT (PARENSPACE|PARENNOSPACE) binop-expr RPAREN

dot-ann : NAME DOT NAME
